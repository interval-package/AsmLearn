# 实验二：查找子串并匹配

[TOC]

## 设计说明

### 需求分析

显示命令行终端，提示内容信息，用户输入目标查找字段，输入目标查找内容。

如果输入为非有效（如输入为空、查找字段长于目标内容），输出错误信息。

如果输入有效，对内容进行查找，如果查找匹配完成，则输出第一次匹配的位置。如果未查找到目标内容，则输出未查找到内容。

在本次实验中，同时实现对于目标字段频次的查找。

### 模块结构

`process_data_times`

核心功能函数，用于判断目标串在源串中出现的频次。

`process_data_pos`

核心功能函数，计算目标串在源串第一次出现的位置。

`input_source`

输入功能函数，打印输入提示信息，引导输入。

将输入信息放到源串缓冲区

`input_tar`

输入功能函数，打印输入提示信息，引导输入。

将输入信息放到目标缓冲区

`certify_input`

输入有效性验证，判断是否有非法的输入发生（如空输入，目标大于源）

`end_judge`

最后结论判别阶段，判断是否找到的匹配信息。如果未找到则打印失败信息，如果找到了，则打印对应的位置。

`input_next`

提示函数，提示用户进入下一次运行。

`disp_num`

显示函数，将数据以十进制的方式打印到终端。

`printf`

打印终端信息函数。

| param | type | description                          |
| ----- | ---- | ------------------------------------ |
| ax    | ad   | 将目标传入串的地址放在ax里面进行传输 |
| 作用  |      | 方便进行内容的打印                   |



## 算法设计

算法的基本思路为：

1. 先提示输入信息引导用户进行内容的输入。
2. 在完成输入之后进行内容验证，判断是否为空，是否有过长现象发生。
3. 如果有异常发生，则打印对应异常内容。
4. 没有异常，则进行匹配过程。
5. 在匹配过程，我们设置了一个_find变量，用来存放是否匹配成功的结果。初始化为0。
6. 将si与di指向两个输入缓冲区，使用较为长串的的长度作为循环次数。
7. 对于每次循环内部，我们使用目标串的长度进行对应位置的扫描匹配。
8. 在向前扫描的过程中，如果不相等，则结束扫描，跳入下次循环。
9. 如果全部匹配成功，则跳入成功处理阶段，将目前对应的位序传入dx，_find设置为1。
10. 回到主程序，判断通过标志变量是否查找到。
11. 进行结果的输出。

### 程序框图

![](D:\Coding\AsmProjects\Dos_Env\pics\match逻辑.png)

## 运行结果

### 概览

![image-20220527093700264](D:\Coding\AsmProjects\Dos_Env\pics\res\match.png)



![image-20220527095942329](D:\Coding\AsmProjects\Dos_Env\pics\res\match_2.png)

### 结果分析

最后成果显示稳定，交互友好，符合实验要求。

## 程序清单

### 源代码

```assembly
; in this function we implement the str match function
; we have two types of match
; the 'times' we'd calc the times of a str apears
; the pos get the pos of the str first occurs

assume cs:code,ds:data,ss:stack

code segment
start:
	mov ax, data	; init regs
	mov ds, ax
	mov ax, stack
	mov ss, ax
	mov sp, 128

work:
	lea ax, input_msg_phrase
	call printf

	call input_source
	
	lea ax, input_msg_tar
	call printf

	call input_tar

	call certify_input

	mov al, _find
	cmp al, 1h
	jne next_time

	lea ax, process_msg_doing
	call printf

	call process_data_pos

	call end_judge

next_time:

	call input_next
	je work

ending:
	mov ax, 4c00h
	int 21H

; proc will use the ax to get the msg
printf proc far
	push dx
	mov dx, ax 
	mov ax, 0900h
	int 21H

	LEA DX,offset CRLF
	MOV AH, 09H					 
	INT 21H

	pop dx
	retf
printf endp

input_source proc far

	LEA DX, source_str
	MOV AH, 0AH
	INT 21H
	mov al, source_str + 1

	LEA DX, CRLF
	MOV AH, 09H
	INT 21H

	retf
input_source endp

input_tar proc far
	LEA DX, destin_str
	MOV AH, 0AH
	INT 21H
	mov al, destin_str + 1

	LEA DX, CRLF
	MOV AH, 09H
	INT 21H
	retf
input_tar endp

input_next proc far
	;	judge the input of the char, and decide next action

	lea ax, input_msg_next
	call printf

	mov ah, 08h
	int 21H

	cmp al, 'C'
	retf
input_next endp

process_data_times proc far
	; load the data
	mov _find, 0h

	mov al, source_str+1
	mov ah, 0h
	mov cx, ax
	
	mov si, 2
	mov dx, 0

	cmping:
		push cx
		push si
		mov al, destin_str+1
		mov ah, 0h
		mov cx, ax
		mov di, 2
		
		subcmp:
			mov ah, source_str[si]
			mov al, destin_str[di]
			cmp ah, al
			jne subcmp_fail
			inc di
			inc si
		loop subcmp

		jmp subcmp_success

		subcmp_next:
			; 将si复位
			pop si
			inc si
			pop cx
	
	loop cmping
	
	audition:
		cmp dx, 0
		ja audition_success
	res:
	retf
	
	subcmp_success:
		inc dx
		jmp subcmp_next

	subcmp_fail:
		jmp subcmp_next

	audition_success:
		mov _find, 1h
		jmp res

	audition_fail:
		jmp res
process_data_times endp

process_data_pos proc far
	mov _find, 0h

	mov al, source_str+1
	mov ah, 0h
	mov cx, ax
	mov si, 2
	; using the ah to store flag of success
	mov dh, 01h

	pos_get_main:
		push cx
		push si
		mov al, destin_str+1
		mov ah, 0h
		mov cx, ax
		mov di, 2

		pos_get_sub:

			mov ah, source_str[si]
			mov al, destin_str[di]
			cmp ah, al
			jne pos_get_fail
			inc di
			inc si
		loop pos_get_sub
		
		jmp pos_get_success

		pos_get_next:
			pop si
			inc si
			pop cx
	loop pos_get_main

	pos_get_res:
		retf

	pos_get_success:
		; after this func we should clean the stack
		pop ax
		pop ax

		mov dx, si
		sub dx, 2
		mov _find, 1h
		jmp pos_get_res

	pos_get_fail:
		jmp pos_get_next
process_data_pos endp

certify_input proc far
	mov _find, 0h

	mov dl, destin_str+1
	cmp dl, 0
	je certify_empty
	mov al, source_str+1
	cmp dl, al
	jnb certify_overflow

	lea ax, certification_valid
	call printf
	mov _find, 1h

	certify_end:
		retf

	certify_empty:
		lea ax, certification_empty
		jmp certify_end

	certify_overflow:
		lea ax, certification_overflow
		call printf
		jmp certify_end
certify_input endp

end_judge proc far
	push dx
	mov al, _find
	cmp al, 0h
	ja judge_success

	judge_fail:
		pop dx
		lea ax, disp_audition_fail
		call printf
		jmp judge_res

	judge_success:
		lea dx, disp_audition_success
		mov ah, 09h
		int 21H

		pop dx
		call disp_num
		jmp judge_res

	judge_res:
		retf
end_judge endp

disp_num proc far
	; this proc attemp to disp a sepecific num
	mov si, 0h
	mov ax, dx
	mov ah, 0h
	num_div:
		mov ah, 0h

		div ten
		push ax
		inc si

		cmp al, 0h
		jne num_div

	mov cx, si
	_disp:
		pop dx
		mov dl, dh
		add dl, '0'
		mov ah, 02h
		int 21H
		loop _disp
	LEA DX, CRLF
	MOV AH, 09H
	INT 21H

	retf
disp_num endp

code ends

data segment

	input_msg_phrase db "Input the String of phrase:", '$'

	input_msg_tar db "Input the String of your target", '$'

	input_msg_next db "Press C(Upper case) to continue this func", '$'

	process_msg_doing db "Process the info...", '$'

	disp_audition_success db "Audition finish, calc result: ", '$'

	disp_audition_fail db "Audition finish, no match.", '$'

	certification_empty db "Get empty input, please check.", '$'

	certification_overflow db "Get destinated string longger than source.", '$'

	certification_valid db "Get valid inputs.", '$'

	source_str db 200
		db ?
		db 200 DUP(0), '$'

	destin_str db 20
		db ?
		db 20 DUP(0), '$'

	CRLF DB 0AH, 0DH,'$' 

	_find db 0

	ten db 10
data ends

stack segment
	dw 256 dup(0)
stack ends

end start

```

## 总结

### 实验心得

第一次做汇编实验，主要是要熟悉汇编语言的编辑、汇编、连接及调试的全过程，重点掌握使用DEBUG调试程序的方法。由于是第一次，我没有准备好，对汇编语言的一系列命令不懂，让我很难做，但在可后通过复习和同学的帮助，我慢慢的清楚了汇编语言的编辑和汇编，以及调试的全过程。下次我会在实验前做好充足的准备。

在本次实验中学习到了，汇编语言中中断程序的调用方法，与在内存中数据的分配方法。

学习到了汇编语言的基本用法与功能，实现了第一个小程序。