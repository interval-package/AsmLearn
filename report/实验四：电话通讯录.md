# 实验四：电话通讯录

[TOC]

## 设计说明

### 需求分析

在本次实验中，要求基于汇编语言实现一个带有名字与电话信息的通讯录。

有以下要求：

- 表项保存两个主要内容，电话号码与用户姓名。
- 检测电话号码的长度，如果不满足8位则拒绝输入。
- 将记录的姓名，按照字典序进行排序。
- 用户可以通过查询姓名，获得电话号码。

## 算法设计

- 在本实验中，我们使用两个表来存放电话与姓名。
- 电话表按照输入顺序进行存放，不进行修改，而姓名表的插入的时候进行重排插入。
- 为了使得姓名表能够与电话表进行匹配，我们在姓名表的表项中加入了其对应电话号码在电话表里面的位序。
- 在插入姓名的时候，我们先对姓名表进行一次遍历，找到对应的位置，然后插入到该位置，该位置之后的内容向后移动。

### 模块程序

`core_sort_input`

核心算法函数，排序插入函数，找到姓名在表中对应的位置，然后插入，之后，后面的内容全部向后移动一位。

`input_add_item`

在添加信息阶段，显示提示输入信息，并且接受信息，判断有效性，调用排序插入。

`core_search`

核心函数，查询阶段的搜索函数，会遍历姓名表，获得查找结果。

如果查找成功打印姓名，并且根据索引再查找电话号码，打印电话号码。

`input_search`

在查询阶段，提示输入将要查询的信息。

`compare_name`

姓名比较函数，按照字典序进行比较。

`swap_name`

将缓冲区的内容与目标id处的姓名进行交换。

`save_tel`

保存电话信息，先进行电话号码有效性的判断。

`locate_tel_id`

传入一个数字，将该数字对应索引的电话号码地址返回。

`locate_name_id`

传入一个数字，将该数字对应索引的姓名地址返回。

`disp_total_info`

遍历所有的表，打印遍历内容。

`next_phase`

下一阶段判断函数，检测用户键盘输入，判断下一阶段进行何种功能。

`inputf`

输入函数，传入地址于ax，提示输入，然后将内容放到地址处。

`disp_num`

显示函数，将数据以十进制的形式打印到终端。

`printf`

显示函数，用于在终端打印目标输出。

------

<img src="D:\Coding\AsmProjects\Dos_Env\pics\tel逻辑.png" style="zoom:67%;" />

## 运行结果

### 概览

![image-20220527094347930](D:\Coding\AsmProjects\Dos_Env\pics\res\calc_1.png)

![image-20220527094451018](D:\Coding\AsmProjects\Dos_Env\pics\res\calc_2.png)

### 结果分析

程序运行稳定，功能丰富多样，符合设计需求。

## 程序清单

### 源代码

```assembly
;
; I acknowledge that I do not really think a lot about effectiveness
; that the algorithm may not be best 
;

assume cs:code,ds:data,ss:stack

stack segment
	dw 1024 dup(0)
stack ends

code segment
	start:
		mov ax, data	; init regs
		mov ds, ax
		mov ax, stack
		mov ss, ax
		mov sp, 1024

	to_next_phase:
		call next_phase
		cmp al, 'q'
		je end_phase
		cmp al, 's'
		je search_phase
		cmp al, 't'
		je disp_all_phase
		jmp add_phase

	end_phase:
		mov ax, 4c00h
		int 21H

	search_phase:
		call input_search
		jmp to_next_phase

	disp_all_phase:
		call disp_total_info
		jmp to_next_phase

	add_phase:
		call input_add_item
		jmp to_next_phase

core_sort_input proc far
	; this func will sort and save info

	mov al, tel_num
	sub al, 1
	mov temp_id_ptr, al

	; get the current num of array
	mov al, name_num
	mov ah, 0
	cmp al, ah
	je sort_empty
	mov cx, ax
	mov dx, 0

	sort_main:
		mov ax, dx
		call compare_name
		jna sort_is_low
		jmp sort_loop_end

		sort_loop_end:
			inc dx
	loop sort_main

	; fill the end of table
	mov ax, dx
	call swap_name

	sort_finish:
		; end phase update info
		mov al, name_num
		inc al
		mov name_num, al

		retf

	sort_empty:
		call swap_name
		jmp sort_finish

	sort_is_low:
		mov ax, dx
		call swap_name
		jmp sort_loop_end
core_sort_input endp

input_add_item proc far
	lea ax, msg_add_item
	call printf

	lea ax, msg_input_name
	call printf

	mov ax, offset name_buffer
	call inputf

	lea ax, msg_input_tel
	call printf

	lea ax, tel_buffer
	call inputf

	; here will do the validity judge, and save the tel from buffer
	call save_tel
	jne add_fail

	; and then we try to store the name with sort method
	call core_sort_input

	lea ax, msg_input_success
	call printf

	retf

	add_fail:
		lea ax, msg_input_fail
		call printf
		retf
input_add_item endp

core_search proc far
	mov al, name_num
	mov ah, 0
	mov cx, ax

	mov ax, 0

	search_name:
		; call disp_num
		call compare_name
		je name_find 
		inc ax
	loop search_name

	lea ax, msg_not_find
	call printf

	search_finish:
	retf

	name_find:
		mov dx, ax
		lea ax, msg_find
		call printf

		mov al, dl
		call locate_name_id
		mov ax, di
		add ax, 2
		call printf

		mov al, [di+1]
		call locate_tel_id
		mov ax, di
		add ax, 1
		call printf

		jmp search_finish
core_search endp

input_search proc far
	; call disp_total_info

	lea ax, msg_search_input
	call printf

	lea ax, msg_input_name
	call printf

	lea ax, name_buffer
	call inputf

	call core_search

	retf
input_search endp

compare_name proc far
	; input with index in name table, in al
	; this func would use si, di for comparing
	; this func only returns with flag of cmp and should not change too much out env
	push si
	push di
	push bx
	push cx
	push ax

	; transfer the al to di
	call locate_name_id

	lea si, name_buffer

	; init cx
	mov al, name_buffer+1
	mov ah, 0
	mov cx, ax

	mov bx, 2
	; when comparing using ah buf, al tab
	comp_single:
		mov ah, [bx+si]
		mov al, [bx+di]
		cmp ah, al
		jne comp_end
		inc bx
		loop comp_single

	mov ah, name_buffer+1
	mov al, [di]
	cmp ah, al
	jne comp_end

	comp_end:
		pop ax
		pop cx
		pop bx
		pop di
		pop si
		retf
compare_name endp

swap_name proc far
	push cx
	push dx
	; we assume that we get al of the tar num
	; and this func would swap the num to the buffer

	; get tar pos, in the di
	call locate_name_id

	; swap the len
	mov ah, [di]
	mov al, name_buffer+1
	mov [di], al
	mov name_buffer+1, ah
	

	; cache the temp_ptr
	mov dl, [di+1]
	mov al, temp_id_ptr
	mov temp_id_ptr, dl
	mov [di+1], al

	; swapping the body

	mov ax, 2
	mov si, ax

	; swap all, including the useless data
	mov ax, 20
	mov cx, ax

	add di, 2
	swap_main:
		mov dl, name_buffer[si]
		mov al, [di]
		mov name_buffer[si], al
		mov [di], dl
		inc si
		inc di
		loop swap_main

	pop dx
	pop cx
	retf
swap_name endp

save_tel proc far
	push bx

	tel_valid_judge:
		mov al, tel_buffer+1
		cmp al, eight
		jne tel_end

	; here take caution, add action will change the flag
	mov al, tel_num
	call locate_tel_id
	add di, 1
	mov ax, 2
	mov si, ax
	mov cx, 8

	tel_save:
		mov al, tel_buffer[si]
		mov [di], al
		inc di
		inc si
		loop tel_save

	mov ah, tel_num
	inc ah
	mov tel_num, ah

	mov ax, 0
	cmp ah, al

	tel_end:
		pop bx
		retf
save_tel endp

locate_tel_id proc far
	push dx
	; to locate pos, assume num store in al
	mov ah, 10
	mul ah
	lea dx, tel_tab
	add ax, dx
	mov di, ax
	; return in di
	pop dx
	retf
locate_tel_id endp

locate_name_id proc far
	push dx
	; to locate pos, assume num store in al
	mov ah, 25
	mul ah
	lea dx, name_tab
	add ax, dx
	mov di, ax
	; return in di
	pop dx
	retf
locate_name_id endp

disp_total_info proc far
	push ax
	push cx
	push dx

	mov al, tel_num
	mov ah, 0
	mov cx, ax

	mov ax, 0

	cmp ax, cx
	je disp_total_end

	mov dx, 0

	disp_total_loop:
		mov al, dl
		call locate_name_id
		mov ax, [di]
		call disp_num
		mov ax, [di+1]
		call disp_num
		mov ax, di
		add ax, 2
		call printf

		mov al, dl
		call locate_tel_id
		mov ax, di
		inc ax
		call printf

		inc dl
	loop disp_total_loop

	disp_total_end:
		pop dx
		pop cx
		pop ax
		retf
disp_total_info endp

next_phase proc far
	; judge the input of the char, and decide next action

	lea ax, msg_next_phase
	call printf

	mov ah, 08h
	int 21H
	retf
next_phase endp

inputf proc far
	push dx
	push bx
	mov dx, ax
	mov ah, 0AH
	int 21H

	mov bx, dx
	mov al, [bx+1]
    ADD AL,2
    MOV AH, 0
    add bx, AX
    mov al, '$'
    MOV [bx], al

	lea dx, CRLF
	mov ah, 09h
	int 21H
	pop bx
	pop dx
	retf
inputf endp

printf proc far
	push dx
	push ax
	mov dx, ax 
	mov ax, 0900h
	int 21H

	LEA DX,offset CRLF
	MOV AH, 09H
	INT 21H

	pop ax
	pop dx
	retf
printf endp

disp_num proc far
	push cx
	push si
	push dx
	push ax

	; input with al
	; this proc attempt to disp a specific num
	mov si, 0h
	num_div:
		mov ah, 0h

		div ten
		push ax
		inc si

		cmp al, 0h
		jne num_div

	mov cx, si
	_disp_1:
		pop dx
		mov dl, dh
		add dl, '0'
		mov ah, 02h
		int 21H
		loop _disp_1

	LEA DX, CRLF
	MOV AH, 09H
	INT 21H

	pop ax
	pop dx
	pop si
	pop cx
	retf
disp_num endp

code ends

data segment
	msg_add_item db "Now is to register your tel.", '$'
	msg_search_input db "Now is search phase.", '$'

	msg_input_name db "Input name:", '$'
	msg_input_tel db "Input tel number:", '$'
	msg_input_fail db "Error input!", '$'
	msg_input_success db "Success input", '$'
	
	msg_find db "Find info.", '$'
	msg_not_find db "Sorry not find.", '$'
	msg_next_phase db "Input q to quit, s to search_phase info, t to disp tab in memory.", 0AH, 0DH ,"others to continue.", '$'

	name_buffer db 250
	db ?
	db 250 dup('1'), '$'

	tel_buffer db 16
	db ?
	db 16 dup(0), '$'

	temp_id_ptr db 00h

	; num of current name tab
	name_num db 00h

	; pos 1 stores the length of name
	; pos 2 stores the index of tel in tab
	; so the len of each item is 25

	name_tab db 50 dup(255,0,"1234567890123456789000$")

	; num of current tel tab
	tel_num db 00h
	; the length of tel is fixed 8, so the length of each item is 10
	; pos 1 stores the index of name in tab

	tel_tab db 50 dup(?,"12345678",'$')

	eight db 08h

	ten db 0ah

	CRLF DB 0AH, 0DH,'$' 
data ends

end start
```

## 总结

### 实验心得

软件导致编译运行老是出错，最后是代码上的错误。所以通过这次试验不仅让我懂得对汇编操作的基本过程熟悉了，更多的是对整个编写代码的流程熟悉了。中间总是出现错误时感觉不知道该怎么办了，但是觉得这种代码也并不是很难，应该可以实现，所以通过请教同学、翻阅资料、经过不断地改动之后最终让正确的实验结果出现。所以这次试验还让我对做实验增加了耐心，因而这次收获很多。

在本次实验中实现了电话通讯簿的功能，深度了解了汇编语言的编写过程，了解了字符串的操作与处理与数据io的操作。

这次实验主要是掌握DOS 提供的有关输入/输出的系统功能调用。输入字符和输出字符，在调试过程中，有时文件名打错也会出错，多一个空格也会出错。同时也让我体会到了汇编语言的精准以及DOS的快速性。除此之外。我对汇编语言的调试等都有了较好的了解。